#INLINE F90_GLOBAL
REAL(dp) :: kCl2_g_aq, kCl2_aq_g, kCl_g_aq, kCl_aq_g, kClO_g_aq, kClO_aq_g, kClO2_g_aq, & 
kClO2_aq_g, kHCl_g_aq, kHCl_aq_g, kHOCl_g_aq, kHOCl_aq_g, kClNO_g_aq, kClNO_aq_g, &
kClNO2_g_aq, kClNO2_aq_g, kClNO3_g_aq, kClNO3_aq_g, kBr2_g_aq, kBr2_aq_g, kBr_g_aq, & 
kBr_aq_g, kBrO_g_aq, kBrO_aq_g, kHBr_g_aq, kHBr_aq_g, kHOBr_g_aq, kHOBr_aq_g, kBrNO2_g_aq, &
kBrNO2_aq_g, kBrNO3_g_aq, kBrNO3_aq_g, kBrCl_g_aq, kBrCl_aq_g, kI2_g_aq, kI2_aq_g, kI_g_aq, &
kI_aq_g, kIO_g_aq, kIO_aq_g, kOIO_g_aq, kOIO_aq_g, kI2O2_g_aq, kI2O2_aq_g, kHI_g_aq, &
kHI_aq_g, kHOI_g_aq, kHOI_aq_g, kHIO3_g_aq, kHIO3_aq_g, kINO2_g_aq, kINO2_aq_g, kINO3_g_aq, &
kINO3_aq_g, kICl_g_aq, kICl_aq_g, kIBr_g_aq, kIBr_aq_g, kO3_g_aq, kO3_aq_g, kOH_g_aq, &
kOH_aq_g, kH2O2_g_aq, kH2O2_aq_g, kDMS_g_aq, kDMS_aq_g, kDMSO_g_aq, kDMSO_aq_g, &
kDMSO2_g_aq, kDMSO2_aq_g, kMSIA_g_aq, kMSIA_aq_g, kHPMTF_g_aq, kHPMTF_aq_g, kSO2_g_aq, &
kSO2_aq_g, kHO2_g_aq, kHO2_aq_g, kNO3_g_aq, kNO3_aq_g, kRO2_g_aq, kRO2_aq_g, kHCHO_g_aq, &
kHCHO_aq_g, kNO2_g_aq, kNO2_aq_g, kO2_g_aq, kO2_aq_g, kSO3_g_aq, kSO3_aq_g, &
kNH3_g_aq, kNH3_aq_g, cw, Na, H_ion

#ENDINLINE {above lines go into MODULE KPP_ROOT_Global}
#INCLUDE atoms

#INLINE F90_RCONST
#ENDINLINE

#DEFVAR
DUMMY = IGNORE ;
Cl2 = IGNORE ;
Cl2aq = IGNORE ;
Claq = IGNORE ;
Cl = IGNORE ;
ClO = IGNORE ;
ClOaq = IGNORE ;
ClO2 = IGNORE ;
ClO2aq = IGNORE ;
HCl = IGNORE ;
HClaq = IGNORE ;
HOCl = IGNORE ;
HOClaq = IGNORE ;
CLNO = IGNORE ;
ClNOaq = IGNORE ;
CLNO2 = IGNORE ;
ClNO2aq = IGNORE ;
ClNO3 = IGNORE ;
ClNO3aq = IGNORE ;
Br2 = IGNORE ;
Br2aq = IGNORE ;
Br = IGNORE ;
Braq = IGNORE ;
BrO = IGNORE ;
BrOaq = IGNORE ;
HBr = IGNORE ;
HBraq = IGNORE ;
HOBr = IGNORE ;
HOBraq = IGNORE ;
BrNO2 = IGNORE ;
BrNO2aq = IGNORE ;
BrNO3 = IGNORE ;
BrNO3aq = IGNORE ;
BrCl = IGNORE ;
BrClaq = IGNORE ;
IODINE2 = IGNORE ;
IODINE2aq = IGNORE ;
IODINE = IGNORE ;
IODINEaq = IGNORE ;
IO = IGNORE ;
IOaq = IGNORE ;
OIO = IGNORE ;
OIOaq = IGNORE ;
I2O2 = IGNORE ;
I2O2aq = IGNORE ;
HI = IGNORE ;
HIaq = IGNORE ;
HOI = IGNORE ;
HOIaq = IGNORE ;
HIO3 = IGNORE ;
HIO3aq = IGNORE ;
INO2 = IGNORE ;
INO2aq = IGNORE ;
INO3 = IGNORE ;
INO3aq = IGNORE ;
ICl = IGNORE ;
IClaq = IGNORE ;
IBr = IGNORE ;
IBraq = IGNORE ;
O3 = IGNORE ;
O3aq= IGNORE ;
Clion = IGNORE ;
Cl2ion = IGNORE ;
Cl3ion = IGNORE ;
O2ion = IGNORE ;
//Hion = IGNORE ;
ClOion = IGNORE ;
ClO2ion = IGNORE ;
OHion = IGNORE ;
HSO3ion = IGNORE ;
SO3ion = IGNORE ;
SO32ion = IGNORE ;
HSO4ion = IGNORE ;
SO42ion = IGNORE ;
H2O2 = IGNORE ;
H2O2aq = IGNORE ;
HO2 = IGNORE ;
HO2aq = IGNORE ;
HO2ion = IGNORE ;
OH = IGNORE ;
OHaq = IGNORE ;
HSO5ion = IGNORE ;
SO5ion = IGNORE ;
ClOHion = IGNORE ;
SO4ion = IGNORE ;
Br2ion = IGNORE ;
Brion = IGNORE ;
BrOion = IGNORE ;
Br3ion = IGNORE ;
BrO2aq = IGNORE ;
BrO2ion = IGNORE ;
BrOHion = IGNORE ; 
HBrO2aq = IGNORE ; 
BrO3ion = IGNORE ;
Br2O4aq = IGNORE ;
BrClion = IGNORE ;
Br2Clion = IGNORE ;
BrCl2ion = IGNORE ;
Iion = IGNORE ;
I2ion = IGNORE ;
I3ion = IGNORE ;
HIO2aq = IGNORE ;
IOion = IGNORE ;
IO2ion = IGNORE ;
IO3ion = IGNORE ;
ICl2ion = IGNORE ;
IBr2ion = IGNORE ;
ICLBrion = IGNORE ;
NO3aq = IGNORE ;
NO3 = IGNORE ;
NO2ion = IGNORE ;
NO3ion = IGNORE ;
NO2aq = IGNORE ;
NO2 = IGNORE ;
DMSaq = IGNORE ;
DMS = IGNORE ;
DMSOaq = IGNORE ;
DMSO = IGNORE ;
DMSO2aq = IGNORE ;
DMSO2 = IGNORE ;
CH3SCH3Claq = IGNORE ;
CH3SCH3Braq = IGNORE ;
MSIAaq = IGNORE ;
MSIA = IGNORE ;
HPMTFaq = IGNORE ;
HPMTF = IGNORE ;
DMSOion = IGNORE ;
CH3SOCH3Claq = IGNORE ;
CH3SOCH3Braq = IGNORE ;
CH3SOCH3OHaq = IGNORE ;
CH3SO2CH2aq = IGNORE ;
CH3SO2CH2O2aq = IGNORE ;
SO2aq = IGNORE ;
SO2 = IGNORE ;
//RO2aq = IGNORE ;
//RO2 = IGNORE ;
HCHOaq = IGNORE ;
HCHO = IGNORE ;
CH3SO2aq = IGNORE ;
CH3SO3Haq = IGNORE ;
CH3SO3H2aq = IGNORE ;
CH3SO2ion = IGNORE ;
CH3SO3ion = IGNORE ;
CH2SO3Haq = IGNORE ;
CH2SO3ion = IGNORE ;
CH3SO3aq = IGNORE ;
CH3SO2O2aq = IGNORE ;
O2CH2SO3Haq =  IGNORE ;
O2CH2SO3ion =  IGNORE ;
O2aq = IGNORE ;
O2= IGNORE ;
SO3aq = IGNORE ;
SO3 = IGNORE;
NH3 = IGNORE ;
NH3aq = IGNORE ;
NH4ion = IGNORE ;


#DEFFIX
Hion = IGNORE ;

#INLINE F90_RCONST
#ENDINLINE 
{above lines go into the SUBROUTINES UPDATE_RCONST and UPDATE_PHOTO}
#EQUATIONS


// Gas-particle partitioning:
Cl2= Cl2aq : kCl2_g_aq; // H1 
Cl2aq= Cl2 : kCl2_aq_g; // H1
Cl= Claq : kCl_g_aq; // H2 
Claq= Cl : kCl_aq_g; // H2
ClO= ClOaq : kClO_g_aq; // H3 
ClOaq= ClO : kClO_aq_g; // H3
ClO2= ClO2aq : kClO2_g_aq; // H4 
ClO2aq= ClO2 : kClO2_aq_g; // H4
HCl= HClaq : kHCl_g_aq; // H5 
HClaq= HCl : kHCl_aq_g; // H5
HOCl= HOClaq : kHOCl_g_aq; // H6 
HOClaq= HOCl : kHOCl_aq_g; // H6

CLNO= ClNOaq : kClNO_g_aq; // H7 
ClNOaq= ClNO : kClNO_aq_g; // H7
CLNO2= ClNO2aq : kClNO2_g_aq; // H8 
ClNO2aq= ClNO2 : kClNO2_aq_g; // H8
ClNO3= ClNO3aq : kClNO3_g_aq; // H9 
ClNO3aq= ClNO3 : kClNO3_aq_g; // H9

Br2= Br2aq : kBr2_g_aq; // H15 
Br2aq= Br2 : kBr2_aq_g; // H15
Br= Braq : kBr_g_aq; // H16 
Braq= Br : kBr_aq_g; // H16
BrO= BrOaq : kBrO_g_aq; // H17 
BrOaq= BrO : kBrO_aq_g; // H17
HBr= HBraq : kHBr_g_aq; // H18 
HBraq= HBr : kHBr_aq_g; // H18
HOBr= HOBraq : kHOBr_g_aq; // H19 
HOBraq= HOBr : kHOBr_aq_g; // H19
BrNO2= BrNO2aq : kBrNO2_g_aq; // H20 
BrNO2aq= BrNO2 : kBrNO2_aq_g; // H20
BrNO3= BrNO3aq : kBrNO3_g_aq; // H21 
BrNO3aq= BrNO3 : kBrNO3_aq_g; // H21
BrCl= BrClaq : kBrCl_g_aq; // H22 
BrClaq= BrCl : kBrCl_aq_g; // H22

IODINE2= IODINE2aq : kI2_g_aq; // H28 
IODINE2aq= IODINE2 : kI2_aq_g; // H28
IODINE= IODINEaq : kI_g_aq; // H29 
IODINEaq= IODINE : kI_aq_g; // H29
IO= IOaq : kIO_g_aq; // H30
IOaq= IO : kIO_aq_g; // H30
OIO= OIOaq : kOIO_g_aq; // H31
OIOaq= OIO : kOIO_aq_g; // H31
I2O2= I2O2aq : kI2O2_g_aq; // H32
I2O2aq= I2O2 : kI2O2_aq_g; // H32
HI= HIaq : kHI_g_aq; // H33
HIaq= HI : kHI_aq_g; // H33
HOI= HOIaq : kHOI_g_aq; // H34
HOIaq= HOI : kHOI_aq_g; // H34
HIO3= HIO3aq : kHIO3_g_aq; // H35
HIO3aq= HIO3 : kHIO3_aq_g; // H35
INO2= INO2aq : kINO2_g_aq; // H36
INO2aq= INO2 : kINO2_aq_g; // H36
INO3= INO3aq : kINO3_g_aq; // H37
INO3aq= INO3 : kINO3_aq_g; // H37
ICl= IClaq : kICl_g_aq; // H38
IClaq= ICl : kICl_aq_g; // H38
IBr= IBraq : kIBr_g_aq; // H39
IBraq= IBr : kIBr_aq_g; // H39

O3= O3aq : kO3_g_aq;
O3aq= O3 : kO3_aq_g;
OH= OHaq : kOH_g_aq;
OHaq= OH : kOH_aq_g;
H2O2= H2O2aq : kH2O2_g_aq;
H2O2aq= H2O2 : kH2O2_aq_g;
DMS= DMSaq : kDMS_g_aq;
DMSaq= DMS : kDMS_aq_g;
DMSO= DMSOaq : kDMSO_g_aq;
DMSOaq= DMSO : kDMSO_aq_g;
DMSO2= DMSO2aq : kDMSO2_g_aq;
DMSO2aq= DMSO2 : kDMSO2_aq_g;

MSIA= MSIAaq : kMSIA_g_aq;
MSIAaq= MSIA : kMSIA_aq_g;
HPMTF= HPMTFaq : kHPMTF_g_aq;
HPMTFaq= HPMTF : kHPMTF_aq_g;
SO2= SO2aq : kSO2_g_aq;
SO2aq= SO2 : kSO2_aq_g;

HO2= HO2aq : kHO2_g_aq;
HO2aq= HO2 : kHO2_aq_g;
NO3= NO3aq : kNO3_g_aq;
NO3aq= NO3 : kNO3_aq_g;
//RO2= RO2aq : kRO2_g_aq;
//RO2aq= RO2 : kRO2_aq_g;
HCHO= HCHOaq : kHCHO_g_aq;
HCHOaq = HCHO : kHCHO_aq_g;
NO2= NO2aq : kNO2_g_aq;
NO2aq = NO2 : kNO2_aq_g;
O2= O2aq : kO2_g_aq;
O2aq = O2 : kO2_aq_g;
SO3= SO3aq : kSO3_g_aq;
SO3aq = SO3 : kSO3_aq_g;
NH3= NH3aq : kNH3_g_aq; 
NH3aq= NH3 : kNH3_aq_g;

// Na = Avogadros number, cw = liquid water conc (kg/(cm^3 air))
// Aqueous phase Cl chemistry (kg mol^-1 s^-1) * (cm^3 kg^-1) * (mol molec^-1) = (cm^3 molec^-1 s^-1)   
Claq + Claq = Cl2aq : 8.75D7/(cw*Na) ; // A1  
Cl2ion + Claq = CL2aq + Clion : 2.1D9/(cw*Na) ; // A2
Cl2ion + Cl2ion = Cl2aq + Clion + Clion : 1.8D9/(cw*Na) ; // A3
Clion + O3aq = ClOion : 3D-3/(cw*Na) ; // A4
Claq + H2O2aq = Hion + Clion + HO2aq : 2D9/(cw*Na) ; // A5
Cl2ion + H2O2aq = Clion + Clion + Hion + HO2aq : 5D4*EXP(-3340.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A6
Cl2ion = Hion + Clion + ClOHion : 23.4*55.6; // A7  

Cl2ion + HO2aq = Clion + Clion + Hion : 1.3D10/(cw*Na) ;  // A8
Cl2ion + O2ion = Clion + Clion : 6D9/(cw*Na) ; // A9
Cl2ion + OHaq  = HOClaq + Clion : 1D9/(cw*Na) ; // A10
Cl2ion + OHion = Clion + Clion + OHaq : 4D6/(cw*Na) ; // A11
Cl3ion + HO2aq = Cl2ion + Hion + Clion : 1D9/(cw*Na) ; // A12
Cl3ion + O2ion = Cl2ion + Clion : 3.8D9/(cw*Na) ; // A13
Cl2aq + HO2aq = Cl2ion + Hion : 1D9/(cw*Na) ; // A14
Cl2aq + O2ion = Cl2ion : 1D9/(cw*Na) ; // A15
HOClaq + H2O2aq = Hion + Clion : 1.1D4/(cw*Na) ; // A16
ClOion + H2O2aq = Clion : 1.7D5/(cw*Na) ; // A17
HOClaq + HO2aq = Claq : 7.5D6/(cw*Na) ; // A18
HOClaq + O2ion = Claq + OHion : 7.5D6/(cw*Na) ; // A19
ClOion + O2ion = Claq + OHion + OHion : 2D8/(cw*Na) ; // A20
HOClaq + OHaq = ClOaq : 2D9/(cw*Na) ; // A21
ClOion + OHaq = ClOaq + OHion : 8.8D9/(cw*Na) ; // A22
Cl2ion + HSO3ion = Clion + Clion + Hion + SO3ion : 1.7D8*EXP(-400.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A23
Cl2ion + SO32ion = Clion + Clion + SO3ion : 6.2D7/(cw*Na) ; // A24
HOClaq + SO32ion = Clion + HSO4ion : 7.6D8/(cw*Na) ; // A25
HOClaq + HSO3ion = Clion + Hion + HSO4ion : 7.6D8/(cw*Na) ; // A26 
Clion + HSO5ion = HOClaq + SO42ion : 1.8D-3*EXP(-7352.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A27

CL2ion + NO2ion = Clion + Clion + NO2aq : 6D7/(cw*Na) ;  // A29

Claq + Clion = Cl2ion : 8.5D9/(cw*Na) ; // E1 kf
Cl2ion = Claq + Clion : 6D4 ; // E1 kb
Cl2aq + Clion = Cl3ion : 2D4/(cw*Na) ; // E2 kf
Cl3ion = Cl2aq + Clion : 1.1D5 ; // E2 kb
Cl2aq = Hion + Clion + HOClaq : 55.6*0.4*EXP(-8000.0*(1D0/TEMP-1D0/298D0)) ; // E3 kf
Hion + Clion + HOClaq = Cl2aq : 2.1D4*EXP(-3500.0*(1D0/TEMP-1D0/298D0))/((cw*Na)**2D0) ; // E3 kb 
HClaq = Hion + Clion : 5D11*EXP(6890.0*(1D0/TEMP-1D0/298D0)) ; // E4 kf
Hion + Clion = HClaq: 2.9D5/(cw*Na) ; // E4 kb
HOClaq = Hion + ClOion : 1.5D3 ; // E5 kf
Hion + ClOion = HOClaq : 5D10/(cw*Na) ; // E5 kb
Clion + OHaq = ClOHion : 4.3D9/(cw*Na) ; // E6 kf
ClOHion = Clion + OHaq : 6.1D9 ; // E6 kb
Claq + OHion = ClOHion : 1.8D10/(cw*Na) ; // E7 kf
ClOHion = Claq + OHion : 23D0 ; // E7 kb
ClOHion + Hion = Claq : 2.1D10/(cw*Na) ; // E8 kf
Claq = ClOHion + Hion : 55.6*4.1D3 ; // E8 kb
ClOHion + Clion = Cl2ion + OHion : 1D4/(cw*Na) ; // E9 kf
Cl2ion + OHion = ClOHion + Clion : 4.5D7/(cw*Na) ; // E9 kb
Clion + SO4ion = Claq + SO42ion : 2.52D8/(cw*Na) ; // E10 kf
Claq + SO42ion = Clion + SO4ion : 2.1D8/(cw*Na) ; // E10 kb
Clion + NO3aq = Claq + NO3ion : 3.4D8*EXP(-4300.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // E11 kf
Claq + NO3ion = Clion + NO3aq : 1D8/(cw*Na) ; // E11 kb


Braq + Braq = Br2aq   : 1D9/(cw*Na) ; // A85
Br2ion + Br2ion = Br2aq + Brion + Brion : 1.7D9/(cw*Na) ; // A86
Brion + O3aq = BrOion : 210.0*EXP(-4450.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A87
Braq + HO2aq = Hion + Brion : 1.6D8/(cw*Na) ; // A88
Braq + H2O2aq = Hion + Brion + HO2aq : 4D9/(cw*Na) ;// A89
Br2aq + HO2aq = Hion + Br2ion : 1.1D8/(cw*Na) ;// A90
Br2aq + O2ion = Br2ion : 5.6D9/(cw*Na) ;// A91
Br2aq + H2O2aq = Hion + Hion + Brion + Brion : 1.3D3/(cw*Na) ;// A92
Br2ion + OHaq = Brion + HOBraq : 1D9/(cw*Na) ; // A93
Br2ion  + OHion = Brion + Brion + OHaq : 1.1D4/(cw*Na) ; // A94
Br2ion + HO2aq = Brion + Brion + Hion : 4.4D9/(cw*Na) ; // A95
Br2ion + HO2aq = Br2aq + H2O2aq : 4.4D9/(cw*Na) ; // A96
Br2ion + O2ion = Brion + Brion : 1.7D8/(cw*Na) ; // A97
Br2ion + H2O2aq = Brion + Brion + Hion + HO2aq :  1D5/(cw*Na) ; // A98
Br3ion + HO2aq = Br2ion + Hion + Brion : 1D7/(cw*Na) ; // A99
Br3ion + O2ion = Br2ion + Brion : 3.8D9/(cw*Na) ; // A100
BrOaq + BrOaq = BrO2ion + BrOion + Hion + Hion : 2.8D9/(cw*Na) ; // A101
BrO2ion + BrOaq = BrO2aq + BrOion : 4D8/(cw*Na) ; // A102
Br2ion + BrO2ion = Brion + Brion + BrO2aq : 8D7/(cw*Na) ; // A103
BrO2ion + OHaq = BrO2aq + OHion : 1.8D9/(cw*Na) ; // A104
HOBraq + OHaq = BrOaq : 2D9/(cw*Na) ; // A105
BrOion + OHaq = BrOaq + OHion : 4.5D9/(cw*Na) ; // A106
HOBraq + HO2aq = Braq : 1D9/(cw*Na) ; // A107
HOBraq + O2ion = Braq + OHion : 3.5D9/(cw*Na) ; // A108
BrOion + O2ion = Braq + OHion + OHion : 2D8/(cw*Na) ; // A109
HOBraq + H2O2aq = Hion + Brion : 3.5D6/(cw*Na) ; // A110
BrOion + H2O2aq = Brion : 2D5/(cw*Na) ; // A111
Br2ion + HSO3ion = Brion + Brion + Hion + SO3ion : 5D7*EXP(-780.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A112
Br2ion + SO32ion = Brion + Brion + SO3ion :  3.3D7*EXP(-650.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A113
Brion + SO4ion = Braq + SO42ion : 2.1D9/(cw*Na) ; // A114
HOBraq + SO32ion = Brion + HSO4ion : 5D9/(cw*Na) ; // A115
HOBraq + HSO3ion = Hion + Brion + HSO4ion : 5D9/(cw*Na) ; // A116
Brion + HSO5ion = HOBraq + SO42ion : 1D0*EXP(-5338.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A117

Brion + NO3aq = Braq + NO3ion : 3.8D9/(cw*Na) ; // A119
Br2ion + NO2ion = Brion + Brion + NO2aq : 1.2D7*EXP(-1720.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A120
//Brion + NO2kation = BrNO2aq : 1D10/(cw*Na) ; // A121 
Brion + BrNO2aq = Br2aq + NO2ion : 2.55D4/(cw*Na) ; // A122

Br2ion + Cl2ion = Br2aq + Clion + Clion : 4D9/(cw*Na) ; // A131
Brion + HOClaq = BrClaq : 1.3D6/(cw*Na) ; // A132
Brion + ClOion = BrClaq + OHion : 3.65D10/(cw*Na) ; // A133
Brion + ClNO2aq = BrClaq + NO2ion : 5D6/(cw*Na) ; // A134
BrNO2aq + Clion = BrClaq + NO2ion : 1D1/(cw*Na) ; // A135

Braq + Brion = Br2ion : 1.2D10/(cw*Na) ; // E15 kf
Br2ion = Braq + Brion : 1.9D4 ; // E15 kb
Br2aq + Brion = Br3ion : 9.6D8/(cw*Na) ; // E16 kf
Br3ion = Br2aq + Brion : 5.5D7 ; // E16 kb
Br2aq = Hion + Brion + HOBraq : 55.6*1.7*EXP(-7500.0*(1D0/TEMP-1D0/298D0)) ; // E17 kf
Hion + Brion + HOBraq = Br2aq : 1.6D10/((cw*Na)**2D0) ; // E17 kb
HBraq = Hion + Brion : 5D11 ; // E18 kf
Hion + Brion = HBraq : 5D2/(cw*Na) ; // E18 kb
HOBraq = Hion + BrOion : 1D2 ; // E19 kf
Hion + BrOion = HOBraq : 5D10/(cw*Na) ; // E19 kb
Brion + OHaq = BrOHion : 1.1D10/(cw*Na) ; // E20 kf
BrOHion = Brion + OHaq : 3.3D7 ; // E20 kb
Braq + OHion = BrOHion : 1.3D10/(cw*Na) ; // E21 kf
BrOHion = Braq + OHion : 4.2D6 ; // E21 kb
BrOHion + Hion = Braq : 4.4D10/(cw*Na) ; // E22 kf
Braq = BrOHion + Hion : 55.6*2.45D-2 ; // E22 kb
BrOHion + Brion = Br2ion + OHion : 1.9D8/(cw*Na) ; // E23 kf
Br2ion + OHion = BrOHion + Brion : 2.7D6/(cw*Na) ; // E23 kb
HOBraq + HOBraq = Hion + Brion + HBrO2aq : 2D-5/(cw*Na) ; // E24 kf
Hion + Brion + HBrO2aq = HOBraq + HOBraq : 3D6/((cw*Na)**2D0) ; // E24 kb
HBrO2aq = Hion + BrO2ion : 6.3D5 ; // E25 kf
Hion + BrO2ion = HBrO2aq : 5D10/(cw*Na) ; // E25 kb
HOBraq + HBrO2aq = Hion + Hion + Brion + BrO3ion : 3.2D0/(cw*Na) ; // E26 kf
Hion + Hion + Brion + BrO3ion = HOBraq + HBrO2aq : 2.0D0/((cw*Na)**3D0); // E26 kb
HBrO2aq + HBrO2aq = HOBraq + Hion + BrO3ion : 3D3/(cw*Na) ; // E27 kf
HOBraq + Hion + BrO3ion = HBrO2aq + HBrO2aq : 1D-8/((cw*Na)**2D0) ; // E27 kb
Br2O4aq = Hion + BrO3ion + HBrO2aq : 55.6*2.2D3; // E28 kf
Hion + BrO3ion + HBrO2aq = Br2O4aq : 42.0D0/((cw*Na)**2D0) ; // E28 kb
Br2O4aq = BrO2aq + BrO2aq : 7.4D4 ; // E29 kf
BrO2aq + BrO2aq = Br2O4aq : 1.4D9/(cw*Na) ; // E29 kb

BrClaq = HOBraq + Hion + Clion : 1D5 ; // E31 kf
HOBraq + Hion + Clion = BrClaq : 5.6D9/((cw*Na)**2D0) ; // E31 kb
BrClion = Brion + Claq : 1.9D3 ; // E32 kf
Brion + Claq = BrClion : 1.2D10/(cw*Na) ; // E32 kb
BrClion = Braq + Clion : 6.1D4 ; // E33 kf
Braq + Clion = BrClion : 1D8/(cw*Na) ; // E33 kb
BrClion + Brion = Br2ion + Clion : 8D9/(cw*Na) ; // E34 kf
Br2ion + Clion = BrClion + Brion : 4.3D6/(cw*Na) ; // E34 kb
BrClion + Clion = Cl2ion + Brion : 1.1D2/(cw*Na) ; // E35 kf
Cl2ion + Brion = BrClion + Clion : 4D9/(cw*Na) ; // E35 kb
Br2Clion = BrClaq + Brion : 4.3D5 ; // E36 kf
BrClaq + Brion = Br2Clion : 7.7D9/(cw*Na) ; // E36 kb
Br2Clion = Br2aq + Clion : 3.8D4 ; // E37 kf
Br2aq + Clion = Br2Clion : 5D4/(cw*Na) ; // E37 kb
BrCl2ion = BrClaq + Clion : 1.7D5 ; // E38 kf
BrClaq + Clion = BrCl2ion : 1D6/(cw*Na) ; // E38 kb
BrCl2ion = Brion + Cl2aq : 9D3 ; // E39 kf
Brion + Cl2aq = BrCl2ion : 6D9/(cw*Na) ; // E39 kb
Brion + ClOHion = BrClion + OHion : 1D9/(cw*Na) ; // E40 kf
BrClion + OHion = Brion + ClOHion : 3D6/(cw*Na) ; // E40 kb
BrOHion + Clion = BrClion + OHion : 1.9D8/(cw*Na) ; // E41 kf
BrClion + OHion = BrOHion + Clion : 2D7/(cw*Na) ; // E41 kb


IODINEaq + IODINEaq = IODINE2aq : 1.1D10/(cw*Na) ; // A175
IODINEaq + I2ion = I3ion : 6.5D9/(cw*Na) ; // A176
I2ion + I2ion = I3ion + Iion : 2.5D9/(cw*Na) ; // A177
Iion + O3aq = HOIaq : 2.17D9*EXP(-8790.0*(1D0/TEMP-1D0/298D0))/(cw*Na) ; // A178
IODINE2aq + HO2aq = I2ion + Hion : 6D9/(cw*Na) ; // A179
IODINE2aq + O2ion = I2ion : 6D9/(cw*Na) ; // A180
I3ion + HO2aq = I2ion + Hion + Iion : 2.5D8/(cw*Na) ; // A181
I3ion + O2ion = I2ion + Iion : 2.5D8/(cw*Na) ; // A182
HIO2aq + H2O2aq = Hion + IO3ion : 6D1/(cw*Na) ; // A183
IO2ion + H2O2aq = IO3ion : 6D1/(cw*Na) ; // A184
IOaq + IOaq = HOIaq + HIO2aq : 1.5D9/(cw*Na) ; // A185
IODINE2aq + HSO3ion = Hion + Hion + Iion + Iion + HSO4ion : 1D6/(cw*Na) ; // A186
HOIaq + SO32ion = Iion + HSO4ion : 5D9/(cw*Na) ; // A187
HOIaq + HSO3ion = Hion + Iion + HSO4ion : 5D9/(cw*Na) ; // A188
Iion + IClaq = IODINE2aq + Clion : 1.1D9/(cw*Na) ; // A189
Iion + HOClaq = IClaq : 3.5D11/(cw*Na) ; // A190
Iion + HOBraq = IBraq + OHion : 5D9/(cw*Na) ; // A191
HOIaq + Cl2aq = HIO2aq + Hion + Hion + Clion + Clion : 1D6/(cw*Na) ; // A192
HOIaq + HOClaq = HIO2aq + Hion + Clion : 5D5/(cw*Na) ; // A193
HOIaq + HOBraq = HIO2aq + Hion + Brion : 1D6/(cw*Na) ; // A194
HIO2aq + HOClaq = IO3ion + Clion + Hion + Hion : 1.5D3/(cw*Na); // A195
IO2ion + HOClaq = IO3ion + Clion + Hion : 1.5D3/(cw*Na); // A196
HIO2aq + HOBraq = IO3ion + Brion + Hion + Hion : 1D6/(cw*Na); // A197
IO2ion + HOBraq = IO3ion + Brion + Hion : 1D6/(cw*Na) ; // A198

IODINEaq + Iion = I2ion : 9.1D9/(cw*Na) ; // E44 kf
I2ion = IODINEaq + Iion : 6.7D4 ; // E44 kb
IODINE2aq + Iion = I3ion : 6.2D9/(cw*Na) ; // E45 kf
I3ion = IODINE2aq + Iion : 8.7D6 ; // E45 kb
HIaq = Hion + Iion : 5D11 ; // E46 kf
Hion + Iion = HIaq : 156D0/(cw*Na) ; // E46 kb
HOIaq = Hion + IOion : 1.58D0 ; // E47 kf
Hion + IOion = HOIaq : 5D10/(cw*Na) ; // E47 kb
HOIaq + Hion + Iion = IODINE2aq : 4.4D12/((cw*Na)**2D0) ; // E48 kf
IODINE2aq = HOIaq + Hion + Iion : 3D0 ; // E48 kb
HOIaq + HOIaq = HIO2aq + Hion + Iion : 25D0/(cw*Na) ; // E49 kf
HIO2aq + Hion + Iion = HOIaq + HOIaq : 2D10/((cw*Na)**2D0) ; // E49 kb
HOIaq + HOIaq = IO2ion + Hion + Hion + Iion : 25D0/(cw*Na) ; // E50 kf
IO2ion + Hion + Hion + Iion = HOIaq + HOIaq : 2D10/((cw*Na)**3D0) ; // E50 kb
HIO2aq = Hion + IO2ion : 1.26D9 ; // E51 kf
Hion + IO2ion = HIO2aq : 5D10/(cw*Na) ; // E51 kb
HIO3aq = Hion + IO3ion : 8.5D9 ; // E52 kf
Hion + IO3ion = HIO3aq : 5D10/(cw*Na) ; // E52 kb
HIO2aq + HOIaq = IO3ion + Iion + Hion + Hion : 2.4D2/(cw*Na) ; // E53 kf
IO3ion + Iion + Hion + Hion = HIO2aq + HOIaq : 1.2D3/((cw*Na)**3D0) ; // E53 kb
IO2ion + HOIaq = IO3ion + Iion + Hion : 2.4D2/(cw*Na) ; // E54 kf
IO3ion + Iion + Hion = IO2ion + HOIaq : 1.2D3/((cw*Na)**2D0) ; // E54 kb
IO2ion + IODINE2aq = IO3ion + Iion + Iion + Hion + Hion : 5.5D-5/(cw*Na); // E55 kf
IO3ion + Iion + Iion + Hion + Hion = IO2ion + IODINE2aq : 4.2D8/((cw*Na)**4D0); // E55 kb

IBraq + Iion = IODINE2aq + Brion : 2D9/(cw*Na); // E56 kf
IODINE2aq + Brion = IBraq + Iion : 4.74D3/(cw*Na); // E56 kb
HOIaq + Hion + Clion = IClaq : 2.9D10/((cw*Na)**2D0) ; // E57 kf
IClaq = HOIaq + Hion + Clion : 2.4D6 ; // E57 kb
HOIaq + Hion + Brion = IBraq : 4.1D12/((cw*Na)**2D0) ; // E58 kf
IBraq = HOIaq + Hion + Brion : 8D5 ; // E58 kb
IClaq + Clion = ICl2ion : 4.24D9/(cw*Na) ; // E59 kf
ICl2ion = IClaq + Clion : 5.5D7 ; // E59 kb
IBraq + Brion = IBr2ion : 4.93D6/(cw*Na) ; // E60 kf
IBr2ion = IBraq + Brion : 1.7D5 ; // E60 kb
IClaq + Brion = ICLBrion : 7.7D9/(cw*Na) ; // E61 kf
ICLBrion = IClaq + Brion : 4.3D5 ; // E61 kb
IBraq + Clion = ICLBrion : 5D4/(cw*Na) ; // E62 kf
ICLBrion = IBraq + Clion: 3.8D4 ; // E62 kb


// DMS aquesous phase chemistry, Table S9 in Hoffman et al., PNAS 2016: 
// DMS, DMSO, MSIA and CH3SO3H oxidation in the aquesous phase is driven by O3 and OH oxidation
DMSaq + O3aq = DMSOaq : 8.61D8*EXP(-2600*(1D0/TEMP-1D0/298D0))/(cw*Na); // aD1
DMSaq + OHaq = DMSOaq + HO2aq : 1.9D10/(cw*Na); // aD2
DMSaq + Cl2ion = CH3SCH3Claq + Clion : 3D9/(cw*Na); // aD3
DMSaq + Br2ion = CH3SCH3Braq + Brion : 3.2D9/(cw*Na); // aD4
DMSaq + H2O2aq = DMSOaq : 3.4D-2/(cw*Na) ; // aD5
CH3SCH3Claq + O2aq = DMSOaq + CLOaq : 2.41D3/(cw*Na); // aD6
CH3SCH3Braq + O2aq = DMSOaq + BrOaq : 6.02D2/(cw*Na); // aD7 
DMSOaq + O3aq = DMSO2aq : 3D0/(cw*Na); // aD8 
DMSOaq + OHaq = MSIAaq : 6.65D9*EXP(-1270*(1D0/TEMP-1D0/298D0))/(cw*Na); // aD9 
DMSOaq + SO4ion = DMSOion + SO42ion : 2.97D9*EXP(-1440*(1D0/TEMP-1D0/298D0))/(cw*Na); // aD10
DMSOaq + Claq = CH3SOCH3Claq : 6.3D9/(cw*Na); // aD11
DMSOaq + Cl2ion = CH3SOCH3Claq + Clion : 1.6D7/(cw*Na); // aD12
DMSOaq + H2O2aq = DMSO2aq : 2.75D-6/(cw*Na); // aD13
DMSOion + Brion = CH3SOCH3Braq : 5D9/(cw*Na); // aD14
CH3SOCH3Braq + Brion = DMSOaq + Br2ion : 2.6D8/(cw*Na); // aD15
CH3SOCH3Claq = MSIAaq + HClaq : 55.6*1D7; // aD16
CH3SOCH3OHaq = MSIAaq : 1D7; // aD17
DMSO2aq + OHaq = CH3SO2CH2aq : 1.77D7*EXP(-1690*(1D0/TEMP-1D0/298D0))/(cw*Na); // aD18
DMSO2aq + SO4ion =  CH3SO2CH2aq + SO42ion + Hion :  3.95D6*EXP(-1360*(1D0/TEMP-1D0/298D0))/(cw*Na); // aD19
DMSO2aq + Claq = CH3SO2CH2aq + HClaq : 8.2D5/(cw*Na); // aD20
DMSO2aq + Cl2ion = CH3SO2CH2aq + HClaq + Clion : 8.24D3/(cw*Na); // aD21 
DMSO2aq + O2aq = CH3SO2CH2O2aq : 2D9/(cw*Na); // aD22 
//CH3SO2CH2O2aq + RO2aq = CH3SO2aq + HCHOaq : 7D3/(cw*Na); // aD23
MSIAaq + O3aq = CH3SO3Haq : 3.5D7/(cw*Na); // aD24
MSIAaq + OHaq = CH3SO3H2aq : 6D9/(cw*Na); // aD25

CH3SO2ion + OHaq = CH3SO2aq + OHion : 0.9*1.2D10/(cw*Na); // aD26
CH3SO2ion + OHaq = HSO3ion : 0.1*1.2D10/(cw*Na); // aD26
CH3SO2ion + SO4ion = CH3SO2aq + SO42ion : 1D9/(cw*Na); // aD27
CH3SO2ion + Cl2ion = CH3SO2aq + Clion + Clion : 8D8/(cw*Na); // aD28 
CH3SO2ion + H2O2aq = CH3SO3ion : 1.2D-2/(cw*Na); // aD29
CH3SO2ion + O3aq = CH3SO3ion : 2D6/(cw*Na); // aD30  
CH3SO3H2aq + O2aq = CH3SO3Haq + HO2aq : 1.2D9/(cw*Na); // aD31 
CH3SO3Haq + OHaq = CH2SO3Haq : 1.5D7/(cw*Na); // aD32 
CH3SO3ion + OHaq = CH2SO3ion : 1.29D7*EXP(-2630*(1D0/TEMP-1D0/298D0))/(cw*Na);  // aD33
CH3SO3ion + SO4ion = CH3SO3aq + SO42ion : 1.13D4*EXP(-2490*(1D0/TEMP-1D0/298D0))/(cw*Na);  // aD34
CH3SO3ion + Claq =  CH3SO3aq + Clion : 4.9D5/(cw*Na); // aD35
CH3SO3ion + Cl2ion =  CH3SO3aq + Clion + Clion : 3.89D3/(cw*Na); // aD36
CH3SO2aq + OHaq = CH3SO3Haq : 1D10/(cw*Na); // aD37
CH3SO2aq + O3aq = CH3SO3aq : 1.5D9/(cw*Na); // aD38
CH3SO2aq + SO32ion = CH3SO2ion + SO42ion + Hion + Hion : 1.7D9/(cw*Na); // aD39
CH3SO2aq = SO2aq : 8.3D4; // aD40
CH3SO2aq + O2aq = CH3SO2O2aq : 1.2D9/(cw*Na); // aD41
CH3SO2aq + CH3SO2aq = MSIAaq + CH3SO3Haq : 8D8/(cw*Na); // aD42
CH3SO2O2aq + CH3SO2ion = CH3SO3ion + CH3SO3aq : 6.2D8/(cw*Na); // aD43
CH3SO3aq + CH3SO2ion = CH3SO3ion + CH3SO2aq : 1D8/(cw*Na); // aD44
CH3SO3aq = SO3aq : 8.3D4 ; // aD45
CH3SO3aq + HO2aq = CH3SO3Haq : 8.3D5/(cw*Na); // aD46   
CH2SO3Haq + O2aq = O2CH2SO3Haq : 2D9/(cw*Na); // aD47
CH2SO3ion + O2aq = O2CH2SO3ion : 2D9/(cw*Na); // aD48
O2CH2SO3ion + Hion = HCHOaq + Hion + SO3ion : 7D3/(cw*Na); // aD49 

// Equilibirum DM1.0 reactions (Table S10):
MSIAaq = CH3SO2ion + Hion : 2.5D8; // ! Check MSIA pKa
CH3SO2ion + Hion = MSIAaq : 5D10/(cw*Na); // // ! Check MSIA pKa
CH3SO3Haq = CH3SO3ion + Hion : 3.65D12; // ! Check MSA pKa 
CH3SO3ion + Hion = CH3SO3Haq : 5D10/(cw*Na); // ! Check MSA pKa
O2CH2SO3Haq = O2CH2SO3ion + Hion : 3.65D12;
O2CH2SO3ion + Hion = O2CH2SO3Haq : 5D10/(cw*Na);
Clion + DMSOion = CH3SOCH3Claq : 1D10/(cw*Na) ;
CH3SOCH3Claq = Clion + DMSOion : 3.03D7 ;
DMSOion = CH3SOCH3OHaq + Hion : 55.6*1.25D5;
CH3SOCH3OHaq + Hion = DMSOion  : 5D10/(cw*Na);
 
HPMTFaq + OHaq = SO2aq : 1D10/(cw*Na); // Assumed 

SO2aq = HSO3ion + Hion :  1D5; // Assumed
HSO3ion + Hion = SO2aq :  (1D5/(1.71D-2*EXP(7.04D0*(298D0/TEMP-1D0))))/(cw*Na);

HSO3ion = SO32ion + Hion : 1D5; // Assumed
SO32ion + Hion = HSO3ion : (1D5/(5.99D-8*EXP(3.74D0*(298D0/TEMP-1D0))))/(cw*Na);
HSO4ion = SO42ion + Hion : 1D5; // Assumed
SO42ion + Hion = HSO4ion : (1D5/(1.02D-2*(EXP(8.85D0*(298D0/TEMP-1D0))+25.14*(1D0-298D0/TEMP+log(298D0/TEMP)))))/(cw*Na);

HO2aq = Hion + O2ion : 1D5; // Assumed
Hion + O2ion = HO2aq : (1D5/3.5D-5)/(cw*Na); // Keq=3.5D-5 mol/kg Jacobson, 2005
H2O2aq = Hion + HO2ion : 1D5; // Assumed
Hion + HO2ion = H2O2aq : (1D5/(2.21D-12*EXP(-12.52D0*(298D0/TEMP-1D0))))/(cw*Na); // Keq Jacobson, 2005d

 
// Jacobson Table B.8 
H2O2aq + HSO3ion = HSO4ion + Hion : (H_ion*(7.45D7*EXP(-15.96D0*(298D0/TEMP-1D0)))/(1d0+13d0*H_ion))/(cw*Na);  // R1
HSO3ion + HO2aq = HSO4ion + OHaq : 4.35D5/(cw*Na); // R2
SO32ion + HO2aq = SO42ion + OHaq : 5.65D5/(cw*Na); // R2
HSO3ion + O2ion = HSO4ion + OHaq : 4.35D4/(cw*Na); // R2
SO32ion + O2ion = SO42ion + OHaq : 5.65D4/(cw*Na); // R2
HSO3ion + OHaq = SO5ion : 4.2D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R3
SO32ion + OHaq = SO5ion : 4.6D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R3 
SO2aq + O3aq = HSO4ion + Hion :  2.4D4/(cw*Na); // R4
HSO3ion + O3aq = HSO4ion : 3.7D5*EXP(-18.56D0*(298D0/TEMP-1D0))/(cw*Na);// R4 
SO32ion + O3aq = SO42ion : 1.5D9*EXP(-17.72D0*(298D0/TEMP-1D0))/(cw*Na);// R4
HSO3ion + SO5ion = HSO5ion + SO5ion : 3D5*EXP(-10.4D0*(298D0/TEMP-1D0))/(cw*Na); // R7
SO32ion + SO5ion = HSO5ion + SO5ion : 1.3D7*EXP(-6.71D0*(298D0/TEMP-1D0))/(cw*Na); // R7
HSO3ion + SO4ion = HSO4ion + SO5ion : 1.3D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R8
SO32ion + SO4ion = SO42ion + SO5ion : 5.3D8*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R8
HSO3ion + HSO5ion = HSO4ion + HSO4ion : 7.1D6*EXP(-10.47D0*(298D0/TEMP-1D0))/(cw*Na); // R9
H2O2aq + OHaq = HO2aq : 2.7D7*EXP(-5.7D0*(298D0/TEMP-1D0))/(cw*Na); // R14
H2O2aq + SO4ion = HO2aq + SO42ion + Hion : 1.2D3*EXP(-6.71D0*(298D0/TEMP-1D0))/(cw*Na); // R17
OHaq + HO2aq = DUMMY : 7D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R19
OHaq + O2ion = DUMMY : 1D10*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R19
OHaq + HSO5ion = SO5ion : 1.7D7*EXP(-6.38D0*(298D0/TEMP-1D0))/(cw*Na); // R20
O2ion + O3aq = OHaq : 1.5D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R29
HO2aq + HO2aq = H2O2aq : 8.6D5*EXP(-7.94D0*(298D0/TEMP-1D0))/(cw*Na); // R30
HO2aq + O2ion = H2O2aq : 1D8*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R30
HO2aq + SO4ion = SO42ion + Hion : 5D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R32
O2ion + SO4ion = SO42ion + Hion : 5D9*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R32
O2ion + SO5ion = HSO5ion :  1D8*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R33
SO5ion + SO5ion = SO4ion + SO4ion : 6D8*EXP(-5.03D0*(298D0/TEMP-1D0))/(cw*Na); // R40

NH3aq + Hion = NH4ion : 1D10/(cw*Na); // Assumed
NH4ion = NH3aq + Hion : 1D10/(1.7882D9*exp(21.0200*(298./T-1D0))); // kf/Keq